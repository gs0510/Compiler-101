<program> -> <otherFunctions> <mainFunction>
<mainFunction> -> TK_MAIN <stmts> TK_END
<otherFunctions> -> <function> <otherFunctions>
<otherFunctions> -> eps
<function> -> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> -> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> -> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> -> eps
<parameter_list> -> <dataType> TK_ID <remaining_list>
<dataType> -> <primitiveDatatype>
<dataType> -> <constructedDatatype>
<primitiveDatatype> -> TK_INT
<primitiveDatatype> -> TK_REAL
<constructedDatatype> -> TK_RECORD TK_RECORDID
<remaining_list> -> TK_COMMA <parameter_list>
<remaining_list> -> eps
<stmts> -> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions> -> <typeDefinition> <typeDefinitions>
<typeDefinitions> -> eps
<typeDefinition> -> TK_RECORD TK_RECORDID <fieldDefinitions> TK_ENDRECORD TK_SEM
<fieldDefinitions> -> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> -> TK_TYPE <primitiveDatatype> TK_COLON TK_FIELDID TK_SEM
<moreFields> -> <fieldDefinition> <moreFields>
<moreFields> -> eps
<declarations> -> <declaration> <declarations>
<declarations> -> eps
<declaration> -> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not> -> TK_COLON TK_GLOBAL
<global_or_not> -> eps
<otherStmts> -> <stmt> <otherStmts>
<otherStmts> -> eps
<stmt> -> <assignmentStmt>
<stmt> -> <iterativeStmt>
<stmt> -> <conditionalStmt>
<stmt> -> <ioStmt>
<stmt> -> <funCallStmt>
<assignmentStmt> -> <SingleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM 
<SingleOrRecId> -> TK_ID <isRecord>
<isRecord> -> TK_DOT TK_FIELDID
<isRecord> -> eps
<funCallStmt> -> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> -> TK_SQL <idList> TK_SQR TK_ASSIGNOP 
<outputParameters> -> eps 
<inputParameters> -> TK_SQL <idList> TK_SQR
<iterativeStmt> -> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE 
<conditionalStmt> -> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <continue_else> 
<continue_else> -> TK_ELSE <stmt> <otherStmts> TK_ENDIF
<continue_else> -> TK_ENDIF 
<ioStmt> -> TK_READ TK_OP <SingleOrRecId> TK_CL TK_SEM 
<ioStmt> -> TK_WRITE TK_OP <allVar> TK_CL TK_SEM 
<allVar> -> <var> 
<allVar> -> <SingleOrRecId> 
<arithmeticExpression> -> <arithmeticExpression1> <arithmeticExpression2> 
<arithmeticExpression2> -> <operator1> <arithmeticExpression1> <arithmeticExpression2>
<arithmeticExpression2> -> eps 
<arithmeticExpression1> -> <factorExpression> <arithmeticExpression3> 
<arithmeticExpression3> -> <operator2> <factorExpression> <arithmeticExpression3>
<arithmeticExpression3> -> eps 
<factorExpression> -> TK_OP <arithmeticExpression> TK_CL
<factorExpression> -> <allVar>
<operator1> -> TK_PLUS 
<operator1> -> TK_MINUS
<operator2> -> TK_MUL
<operator2> -> TK_DIV
<booleanExpression> -> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL 
<booleanExpression> -> <allVar> <relationalOp> <allVar>
<booleanExpression> -> TK_NOT <booleanExpression>
<var> -> TK_NUM
<var> -> TK_RNUM
<logicalOp> -> TK_AND
<logicalOp> -> TK_OR
<relationalOp> -> TK_LT
<relationalOp> -> TK_LE
<relationalOp> -> TK_EQ
<relationalOp> -> TK_GT
<relationalOp> -> TK_GE
<relationalOp> -> TK_NE
<returnStmt> -> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> -> TK_SQL <idList> TK_SQR
<optionalReturn> -> eps
<idList> -> TK_ID <more_ids>
<more_ids> -> TK_COMMA <idList>
<more_ids> -> eps
